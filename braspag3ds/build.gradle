plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.kezong.fat-aar'
}

android {
    namespace "br.com.braspag.app"

    compileSdkVersion rootProject.ext.sdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.sdkVersion

        archivesBaseName = "braspag"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField 'String', 'X_SDK_VERSION', "\"3DS-Android@${rootProject.ext.versionName}\""
        buildConfigField 'String', 'APP_CENTER_KEY', "\"897ccc34-4dc4-4255-b7eb-131d9c62b5a3\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    ext.cardinal_url = properties.getProperty('CARDINAL_URL')
    ext.cardinal_user = properties.getProperty('CARDINAL_USER')
    ext.cardinal_key = properties.getProperty('CARDINAL_KEY')
} else {
    ext.cardinal_url = System.getenv('CARDINAL_URL')
    ext.cardinal_user = System.getenv('CARDINAL_USER')
    ext.cardinal_key = System.getenv('CARDINAL_KEY')
}

fataar {
    transitive = true
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.6.0'
    implementation 'org.conscrypt:conscrypt-android:2.2.1'

    implementation fileTree(include: ['*.aar', '*jar'], dir: 'libs')
    implementation project(path: ':lib-cardinal', configuration: 'default')

    //JWT.IO
    implementation 'com.auth0:java-jwt:3.18.1'

    //APPCENTER
    def appCenterSdkVersion = '4.3.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}